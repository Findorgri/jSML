
project.ext {

  cfgVersion = "1.1.2"
  
  cfgGroup = "org.openmuc"

  cfgCopyDependencies = true

  cfgCopyToRoot = false

  cfgSignPom = true

  cfgRepository = project.properties.sonatypeRepository

  cfgSnapshotRepository = project.properties.sonatypeSnapshotRepository

  cfgRepositoryUser = project.properties.sonatypeUser

  cfgRepositoryPass = project.properties.sonatypePass

  javaProjects = allprojects

  distributionProjects = allprojects.findAll {
    it.getPath() == ":"
  }

  docProjects = distributionProjects

  repositoryProjects = distributionProjects
}

tasks.withType(Tar) {

  into(project.name) {
    from("./") {
      include "*.gradle"
      include "license/**"
      include "doc/*.txt"
      include "doc/userguide/" + project.name + "-doc*.html"
      include "doc/userguide/" + project.name + "-doc-img/**"
      include "run-scripts/**"
      include "gradle/"
      include "gradlew*"
      include "build/libs-all/**"
      include "build/docs/javadoc/**"
      include "dependencies/**"
      include "src/**"
      include "cli-app/**"
    }

    if (name.equals("tar") ){
      exclude "**/dependencies/**/src"
      exclude "**/.settings/"
      exclude "**/.gradle/"
      exclude "**/.project/"
      exclude "**/.classpath/"
      exclude "**/bin/"
      exclude "**/tmp/"
    }

  }
}


//------------------project specific configurations--------------------

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "osgi"
apply plugin: "maven"
apply plugin: "signing"


dependencies {
  compile group: "org.openmuc", name: "jrxtx", version: "1.0.1"

  testCompile "org.mockito:mockito-core:1.10.19"
  testCompile "org.powermock:powermock-api-mockito:1.6.6"
  testCompile "org.powermock:powermock-module-junit4:1.6.6"
}

jar {
  manifest {
    name = "jSML"
    instruction "Export-Package", "!*.internal.*,*"
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      pom.project {
        name "jSML"
        description "jSML is a library that can be used to communicate using the Smart Message Language (SML)."
        
        licenses {
          license {
            name 'Mozilla Public License v2.0'
            url 'http://mozilla.org/MPL/2.0/'
            distribution 'repo'
          }
        }
        
      }
    }
  }
}

